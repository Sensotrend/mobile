def isRunningOnTravis = System.getenv("CI") == "true"

buildscript {
  repositories {
    google()
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.+'
  }
}

subprojects {
    afterEvaluate {project ->
        if (project.hasProperty("android")) {
          android {
            compileSdkVersion 27
            buildToolsVersion "27.0.3"
          }
        }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = stdout
        }

        // Multiply by 100 so we could do manual releases, manually bumping vesionCode if needed.
        // When it ends in 00, it should correspond to git commit number on current branch
        return Integer.parseInt(stdout.toString().trim()) * 100
    }
    catch (ignored) {
        return -1;
    }
}

def getPassword(String keyChain) {
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    exec {
        commandLine 'security', '-q', 'find-generic-password', '-s', keyChain, '-w'
        standardOutput = stdout
        errorOutput = stderr
        ignoreExitValue true
    }
    stdout.toString().trim()
}

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'

  defaultConfig {
    applicationId 'io.tidepool.urchin'
    targetSdkVersion 26
    versionCode getVersionCode()
    versionName '3.1.1'
    ndk {
      abiFilters 'armeabi-v7a', 'x86'
    }
    multiDexEnabled true
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    // Deprecated. Used by net.openid:appauth
    manifestPlaceholders = [
      'appAuthRedirectScheme': 'host.exp.exponent'
    ]
  }
  dexOptions {
    javaMaxHeapSize System.getenv("DISABLE_DEX_MAX_HEAP") ? null : "8g"
  }

  flavorDimensions 'minSdk', 'remoteKernel'
  productFlavors {
    devKernel {
      dimension 'remoteKernel'
    }
    prodKernel {
      dimension 'remoteKernel'
    }
    devMinSdk {
      dimension 'minSdk'
      // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
      // to pre-dex each module and produce an APK that can be tested on
      // Android Lollipop without time consuming dex merging processes.
      minSdkVersion 21
    }
    prodMinSdk {
      dimension 'minSdk'
      minSdkVersion 21
    }
  }

  // Configure keystore
  signingConfigs {
    debug {
      storeFile = file('../debug.keystore')
    }
    release {
      storeFile = isRunningOnTravis ? file("../../release-keystore.jks") : file(System.getenv("TIDEPOOL_KEYSTORE_FILE"))
      storePassword = isRunningOnTravis ? System.getenv("keystore_password") : getPassword("tidepool_keystore_store_password")
      signingConfigs.release.keyAlias = isRunningOnTravis ? System.getenv("keystore_alias") : System.getenv("TIDEPOOL_KEYSTORE_KEY_ALIAS")
      signingConfigs.release.keyPassword = isRunningOnTravis ? System.getenv("keystore_alias_password") : getPassword("tidepool_keystore_key_password")
    }
  }

  buildTypes {
    debug {
      debuggable true
      ext.enableCrashlytics = false
      signingConfig signingConfigs.debug
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      zipAlignEnabled true
      signingConfig signingConfigs.release
    }
  }

  lintOptions {
    abortOnError false
  }
  packagingOptions {
    pickFirst "**"
  }
  configurations.all {
    resolutionStrategy.force 'com.android.support:design:27.1.0'
    resolutionStrategy.force 'com.google.android.gms:play-services-gcm:16.0.0'
  }
}

configurations.all {
  resolutionStrategy {
    force 'org.webkit:android-jsc:r224109'
  }
}

// WHEN_PREPARING_SHELL_REMOVE_FROM_HERE

apply from: 'expo.gradle'

// WHEN_PREPARING_SHELL_REMOVE_TO_HERE

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  implementation 'com.android.support:multidex:1.0.1'

  // Our dependencies
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation project(':react-native-device-info')
  implementation project(':rollbar-react-native')
  implementation 'com.rollbar:rollbar-api:1.4.0'
  implementation 'com.rollbar:rollbar-java:1.4.0'
  implementation 'com.rollbar:rollbar-android:1.4.0@aar'

  // Our dependencies from ExpoView
  // DON'T ADD ANYTHING HERE THAT ISN'T IN EXPOVIEW. ONLY COPY THINGS FROM EXPOVIEW TO HERE.
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation 'com.facebook.android:facebook-android-sdk:4.37.0'
  implementation('com.facebook.android:audience-network-sdk:4.99.0') {
    exclude module: 'play-services-ads'
  }
  compileOnly 'org.glassfish:javax.annotation:3.1.1'
  implementation 'com.jakewharton:butterknife:8.4.0'
  implementation 'de.greenrobot:eventbus:2.4.0'

  // Be careful when upgrading! Upgrading might break experience scoping. Check with Jesse. See Analytics.resetAmplitudeDatabaseHelper
  implementation 'com.amplitude:android-sdk:2.9.2'

  implementation 'com.squareup.picasso:picasso:2.5.2'
  implementation 'com.google.android.gms:play-services-gcm:15.0.1'
  implementation 'com.google.android.gms:play-services-analytics:16.0.1'
  implementation 'com.google.android.gms:play-services-maps:15.0.1'
  implementation 'com.google.android.gms:play-services-auth:15.0.1'
  implementation 'com.google.android.gms:play-services-location:15.0.1'
  implementation 'com.google.android.gms:play-services-ads:15.0.1'
  annotationProcessor 'com.raizlabs.android:DBFlow-Compiler:2.2.1'
  implementation "com.raizlabs.android:DBFlow-Core:2.2.1"
  implementation "com.raizlabs.android:DBFlow:2.2.1"
  implementation "com.madgag.spongycastle:core:1.53.0.0"
  implementation "com.madgag.spongycastle:prov:1.53.0.0"
  debugImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
  // debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.4-beta1'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
  implementation 'com.facebook.device.yearclass:yearclass:2.1.0'
  implementation 'commons-io:commons-io:1.4'
  implementation 'me.leolin:ShortcutBadger:1.1.4@aar'
  implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.0'
  implementation 'com.yqritc:android-scalablevideoview:1.0.1'
  implementation 'commons-codec:commons-codec:1.10'
  implementation 'com.segment.analytics.android:analytics:4.3.0'
  implementation 'com.google.zxing:core:3.3.3'
  implementation 'net.openid:appauth:0.4.1'
  implementation('com.airbnb.android:lottie:2.5.5') {
    exclude group: 'com.android.support', module: 'appcompat-v7'
  }
  implementation('io.nlopez.smartlocation:library:3.2.11') {
    transitive = false
  }
  implementation 'com.android.support:exifinterface:27.1.1'
  implementation 'com.squareup.okio:okio:1.9.0'
  implementation 'com.facebook.soloader:soloader:0.5.1'

  implementation 'com.google.android.exoplayer:exoplayer:2.6.1'
  implementation 'expolib_v1.com.google.android.exoplayer:expolib_v1-extension-okhttp:2.6.1@aar'

  // expo-file-system
  implementation 'com.squareup.okhttp3:okhttp:3.10.0'
  implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'

  // Testing
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
  // We use a modified build of com.android.support.test:runner:1.0.1. Explanation in maven-test/README
  androidTestImplementation 'com.android.support.test:runner:1.0.1'
  androidTestImplementation 'com.android.support:support-annotations:27.1.1'
  androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.0'
  androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
  androidTestImplementation 'com.azimolabs.conditionwatcher:conditionwatcher:0.2'

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:1.10.19'
  testImplementation 'org.robolectric:robolectric:3.8'
  testImplementation 'com.android.support.test:runner:1.0.2-alpha1'
  testImplementation 'com.android.support.test:rules:1.0.2-alpha1'

  implementation('host.exp.exponent:expoview:32.0.0@aar') {
    transitive = true
    exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    exclude group: 'com.squareup.okhttp3', module: 'okhttp-urlconnection'
  }

  api 'com.facebook.react:react-native:32.0.0'
  api 'host.exp.exponent:expo-app-loader-provider:1.0.0'
  api 'host.exp.exponent:expo-core:2.0.0'
  api 'host.exp.exponent:expo-constants-interface:2.0.0'
  api 'host.exp.exponent:expo-constants:2.0.1'
  api 'host.exp.exponent:expo-errors:1.0.0'
  api 'host.exp.exponent:expo-file-system-interface:2.0.0'
  api 'host.exp.exponent:expo-file-system:2.0.0'
  api 'host.exp.exponent:expo-image-loader-interface:2.0.0'
  api 'host.exp.exponent:expo-permissions:2.0.0'
  api 'host.exp.exponent:expo-permissions-interface:2.0.0'
  api 'host.exp.exponent:expo-sensors-interface:2.0.0'
  api 'host.exp.exponent:expo-react-native-adapter:2.0.0'
  api 'host.exp.exponent:expo-task-manager:1.0.0'
  api 'host.exp.exponent:expo-task-manager-interface:1.0.0'

  // Optional universal modules, could be removed
  // along with references in MainActivity
  // api 'host.exp.exponent:expo-ads-admob:2.0.0'
  // api 'host.exp.exponent:expo-app-auth:2.0.0'
  // api 'host.exp.exponent:expo-analytics-segment:2.0.0'
  // api 'host.exp.exponent:expo-barcode-scanner-interface:2.0.0'
  // api 'host.exp.exponent:expo-barcode-scanner:2.0.0'
  // api 'host.exp.exponent:expo-camera-interface:2.0.0'
  // api 'host.exp.exponent:expo-camera:2.0.0'
  // api 'host.exp.exponent:expo-contacts:2.0.0'
  // api 'host.exp.exponent:expo-face-detector:2.0.0'
  // api 'host.exp.exponent:expo-face-detector-interface:2.0.0'
  api 'host.exp.exponent:expo-font:2.0.0'
  api 'host.exp.exponent:expo-gl-cpp:2.0.0'
  api 'host.exp.exponent:expo-gl:2.0.0'
  // api 'host.exp.exponent:expo-google-sign-in:2.0.0'
  // api 'host.exp.exponent:expo-local-authentication:2.0.0'
  // api 'host.exp.exponent:expo-localization:2.0.0'
  // api 'host.exp.exponent:expo-location:2.0.1'
  // api 'host.exp.exponent:expo-media-library:2.0.0'
  // api 'host.exp.exponent:expo-print:2.0.0'
  // api 'host.exp.exponent:expo-sensors:2.0.0'
  // api 'host.exp.exponent:expo-sms:2.0.0'
  // api 'host.exp.exponent:expo-background-fetch:1.0.0'
}

// This has to be down here for some reason
apply plugin: 'com.google.gms.google-services'
